plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '1.3.0'
    id 'org.jetbrains.kotlin.jvm' version '1.6.0'
    id 'jacoco'
    id 'org.kordamp.gradle.markdown' version '2.2.0'
}

group 'org.tonybaloney.security'
version '1.25.0'

def ktor_version = "1.6.6"
def kotlin_version = "1.6.0"

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://dl.bintray.com/kotlin/ktor" }
    maven { url "https://dl.bintray.com/kotlin/kotlinx" }
}

dependencies {
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.8.2'
    testImplementation "org.mockito.kotlin:mockito-kotlin:4.0.0"
    testImplementation 'net.bytebuddy:byte-buddy:1.12.2'
    testImplementation 'net.bytebuddy:byte-buddy-agent:1.12.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "io.ktor:ktor-client-core:$ktor_version"
    compile "io.ktor:ktor-client-apache:$ktor_version"
    compile "io.ktor:ktor-client-gson:$ktor_version"
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}

test {
    useJUnitPlatform()
    dependsOn 'cleanTest'
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
// Make the intellij version overridable on the command line to support multiple build versions..
def intellijversion = project.hasProperty('intellijVersion') ? project.getProperty('intellijVersion') : '2021.3'

intellij {
    type = 'PC'
    plugins = ['python-ce', 'yaml']
    updateSinceUntilBuild = false
    version = intellijversion
}

patchPluginXml {
    changeNotes = """
      <h2>1.25.0</h2>
      <ul>
        <li>Added a PyPi API option for package security checking that uses the new PyPI vulnerability API</li>
        <li>Support for 2021.3</li>
        <li>Update safety DB to december </li>
      </ul>
      """
}

publishPlugin {
    token = intellijPublishToken
    // channels 'beta'
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
        csv.enabled false
    }
}

markdownToHtml {
    sourceDir file("doc/checks/")
    outputDir file("src/main/resources/docs")
    fencedCodeBlocks true
}