plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '1.10.0'
    id 'org.jetbrains.kotlin.jvm' version '1.7.22'
    id 'jacoco'
    id 'org.kordamp.gradle.markdown' version '2.2.0'
}

group 'org.tonybaloney.security'
version '1.27.0'

def ktor_version = "1.6.8"

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = 17
    targetCompatibility = 17
}

dependencies {
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.9.0'
    testImplementation "org.mockito.kotlin:mockito-kotlin:4.0.0"
    testImplementation 'net.bytebuddy:byte-buddy:1.12.19'
    testImplementation 'net.bytebuddy:byte-buddy-agent:1.12.19'
    implementation "io.ktor:ktor-client-core:$ktor_version"
    implementation "io.ktor:ktor-client-apache:$ktor_version"
    implementation "io.ktor:ktor-client-gson:$ktor_version"
}

test {
    useJUnitPlatform()
    dependsOn 'cleanTest'
}

intellij {
    type = 'PC'
    plugins = ['python-ce', 'yaml']
    updateSinceUntilBuild = false
    version = '2022.3'
}

patchPluginXml {
    changeNotes = """
      <h2>1.27.0</h2>
      <ul>
        <li>Support for 2022.3</li>
      </ul>
      """
}

publishPlugin {
    token = intellijPublishToken
    // channels 'beta'
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
        csv.enabled false
    }
}

markdownToHtml {
    sourceDir file("doc/checks/")
    outputDir file("src/main/resources/docs")
    fencedCodeBlocks true
}
